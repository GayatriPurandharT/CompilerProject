package src;
import java_cup.runtime.*;


terminal           SEMI, PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN, EQ, 
                   IF, THEN, PRINT, WHILE,  DO, BEGIN,  END;
terminal Integer   INT;
terminal Float     FLOAT;
terminal String    CHAR;
terminal String    ID;
terminal Boolean   BOOL;

non terminal Object             program;
non terminal Astat              statement, assignment, ifthen, print, while, statement_part;
non terminal Lstat              statement_list;
non terminal Aexp               expr, factor, term;

program         ::= statement_list:s
                    {: s.execute(); :};

statement_list  ::= statement_list:l statement_part:s
                    {: RESULT = new Lstat(l,s) ; :}
                |   statement_part:s
                    {: RESULT = new Lstat(s); :}
                ;

statement_part  ::= statement:s
                    {: RESULT = s; :}
                SEMI;

statement       ::= assignment:s
                    {: RESULT = s; :}
                | ifthen:s
                    {: RESULT = s; :}
                | print:s
                    {: RESULT = s; :}
                | while:s
                    {: RESULT = s; :}
                | BEGIN statement_list:s END
                    {: RESULT = Astat.block(s); :}
                ;

while           ::= WHILE expr:e DO statement:s
                    {: RESULT = Astat.whileloop(e,s); :}
                ;

print           ::= PRINT expr:e
                    {: RESULT = Astat.print(e); :}
                ;

ifthen          ::= IF expr:e THEN statement:s
                    {: RESULT = Astat.ifthen(e,s); :}
                ;


assignment      ::= ID:i EQ expr:e
                    {: RESULT = Astat.assignment(i,e); :}
                ;
/*
boolExpr        ::= expr:e1 EQEQ expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.EE); :}
                | expr:e1 NE expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.NE); :}
                | expr:e1 GE expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.GE); :}
                | expr:e1 LE expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.E); :}
                | expr:e1 G expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.G); :}
                | expr:e1 L expr:e2
                    {: RESULT = new Aexp(new Args(e1,e2), sym.L); :}
                ;
*/
expr            ::= expr:e PLUS term:t
                    {: RESULT = new Aexp(new Args(e,t), sym.PLUS); :}
                | expr:e MINUS term:t
                    {: RESULT = new Aexp(new Args(e,t), sym.MINUS); :}
                | term:t
                    {: RESULT = t; :};



term            ::= term:t TIMES factor:f
                    {: RESULT = new Aexp(new Args(t,f), sym.TIMES); :}
                | term:t DIVIDE factor:f
                    {: RESULT = new Aexp(new Args(t,f), sym.DIVIDE); :}
                | factor:f
                    {: RESULT = f; :};

factor          ::= LPAREN expr:e RPAREN
                    {: RESULT = e; :}
                | INT:n
                    {: RESULT = new Aexp(n); :}
                | FLOAT:f
                    {: RESULT = new Aexp(f); :}
                | CHAR:c
                    {:  c = c.substring(1, c.length() - 1);
                         Object a = c; 
                        RESULT = new Aexp(a); :} 
                | BOOL:b
                    {: RESULT = new Aexp(b); :} 
                
                | ID:i
                    {: RESULT = new Aexp(i); :};
